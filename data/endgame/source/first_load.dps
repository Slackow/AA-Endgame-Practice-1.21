/function load[load]:
    /execute unless entity @p run schedule function $ns:load 1
    /execute if entity @p run function:
        /scoreboard objectives add t dummy
        /execute store success score t t run scoreboard objectives add s dummy
        /execute if score t t matches 1.. run function #$ns:first_load

/function init[$ns:first_load]:
    /execute positioned 0 ~ 0 run function:
        /loot replace entity @p weapon.mainhand loot endgame:temple_map
        /function [] with entity @p SelectedItem.components.minecraft:map_decorations.+:
            /tellraw @p [{"text":""},{"text":"AA Endgame Practice by ","color":"yellow"},{"text":"Slackow ","bold":true,"color":"light_purple"},{"text":"[YT]","bold":true,"underlined":true,"color":"red","clickEvent":{"action":"open_url","value":"https://youtube.com/slackow"},"hoverEvent":{"action":"show_text","contents":[{"text":"Go to my YouTube"}]}},{"text":" "},{"text":"[GH]","bold":true,"underlined":true,"color":"gray","clickEvent":{"action":"open_url","value":"https://github.com/slackow"},"hoverEvent":{"action":"show_text","contents":[{"text":"Go to my GitHub"}]}}]
            /$tellraw @a {"text":"Found Temple at $(x) $(z)","color":"yellow"}
            /$spreadplayers $(x) $(z) 14 150 false @a
            /$execute at @p unless biome ~ ~ ~ desert run function [] {x:$(x),z:$(z)}:
                /$spreadplayers $(x) $(z) 14 150 false @a
                /$execute at @p unless biome ~ ~ ~ desert run function $__f {x:$(x),z:$(z)}
            /execute as @p at @s positioned over motion_blocking_no_leaves run function:
                /tp @a ~ ~ ~
                /setworldspawn
                /function $ns:start

/function start[]:
    /loot replace entity @p weapon.mainhand loot endgame:ancient_city_map
    /function [] with entity @p SelectedItem.components.minecraft:map_decorations.+:
        /$forceload add $(x) $(z)
        /$place template endgame:nether_portal $(x) -35 $(z)
        /$forceload remove $(x) $(z)
        /$data modify storage s ac set value {x:$(x),z:$(z)}
        /execute store result storage s ac.nx int 0.125 run data get storage s ac.x
        /execute store result storage s ac.nz int 0.125 run data get storage s ac.z
        /execute store result storage s sp.x int 1 run data get entity @p Pos[0]
        /execute store result storage s sp.z int 1 run data get entity @p Pos[2]
        /tellraw @a {"text":"Placed End Portal above spawn","color":"gray"}
        /function minecraft:coords[]:
            /tellraw @a [{"text":"","color":"green"},{"text":"Spawn: ","color":"yellow"},{"text":"["},{"storage":"s","nbt":"sp.x"},{"text":", "},{"storage":"s","nbt":"sp.z"},{"text":"]"}]
            /function [] with storage s ac:
                /$tellraw @p [{"text":""},{"text":"Placed Portal in Ancient City at ","color":"aqua"},{"text":"[$(x), -35, $(z)]","color":"green","clickEvent":{"action":"suggest_command","value":"$(x), $(z) city"}},{"text":"\nNether Ancient City: ","color":"red"},{"text":"[$(nx), $(nz)]","color":"green","clickEvent":{"action":"suggest_command","value":"$(nx), $(nz) ncity"}}]
                /tellraw @a [{"text":"[Find Monument] ","color":"dark_aqua","clickEvent":{"action":"run_command","value":"/locate structure monument"},"hoverEvent":{"action":"show_text","contents":[{"text":"/locate structure monument"}]}},\
                {"text":"[Find Ocean]\n","color":"blue","clickEvent":{"action":"run_command","value":"/locate biome #endgame:ocean"},"hoverEvent":{"action":"show_text","contents":[{"text":"/locate biome #endgame:ocean"}]}},\
                {"text":"[Find Trial Chambers] ","color":"red","clickEvent":{"action":"run_command","value":"/locate structure trial_chambers"},"hoverEvent":{"action":"show_text","contents":[{"text":"/locate structure trial_chambers"}]}},\
                {"text":"[Find Village] ","color":"gold","clickEvent":{"action":"run_command","value":"/locate structure #village"},"hoverEvent":{"action":"show_text","contents":[{"text":"/locate structure #village"}]}}]
        /tellraw @a "Do /function coords to see this again."
    /difficulty easy
    /xp add @p 24 levels
    /execute positioned ~2 ~ ~2 positioned over motion_blocking_no_leaves run place template endgame:sniffers ~ ~-1 ~
    /place template endgame:end_portal ~-2 ~25 ~-2
    /gamemode survival @a
    /function $ns:inventory

/function inventory[]:
    /clear @p
    /item replace entity @p hotbar.0 with diamond_axe[enchantments={levels:{efficiency:5,silk_touch:1,unbreaking:3}}]
    /item replace entity @p hotbar.1 with diamond_pickaxe[enchantments={levels:{efficiency:5,fortune:3,unbreaking:3}}]
    /item replace entity @p hotbar.2 with water_bucket
    /item replace entity @p hotbar.3 with diamond_shovel[enchantments={levels:{efficiency:4,unbreaking:3}}]
    /item replace entity @p hotbar.4 with netherrack 64
    /item replace entity @p hotbar.6 with diamond_chestplate[enchantments={levels:{protection:4,unbreaking:3}}]
    /item replace entity @p hotbar.7 with firework_rocket 64
    /item replace entity @p hotbar.8 with diamond_sword[enchantments={levels:{smite:5,looting:3,unbreaking:3}}]

    /item replace entity @p armor.head with diamond_helmet[enchantments={levels:{unbreaking:3,respiration:3,aqua_affinity:1,protection:3}}]
    /item replace entity @p armor.chest with elytra[enchantments={levels:{unbreaking:3,mending:1}}]
    /item replace entity @p armor.legs with diamond_leggings[enchantments={levels:{unbreaking:3,swift_sneak:3,protection:4}}]
    /item replace entity @p armor.feet with diamond_boots[enchantments={levels:{unbreaking:3,depth_strider:3,protection:3}}]

    /item replace entity @p inventory.4 with blue_shulker_box[container=[\
        {slot: 0,item:{id:'gold_block',count:64}},\
        {slot: 1,item:{id:'gold_block',count:64}},\
        {slot: 2,item:{id:'gold_block',count:37}},\
        {slot: 3,item:{id:'gold_ingot',count:43}},\
        {slot: 4,item:{id:'conduit',count:1}},\
        {slot: 5,item:{id:'dark_prismarine',count:16}},\
        {slot: 6,item:{id:'honey_bottle',count:3}},\
        {slot: 8,item:{id:'bundle',count:1,components:{bundle_contents:[\
            {id:'netherite_upgrade_smithing_template',count:4},\
            {id:'netherite_ingot',count:3},\
            {id:'netherite_scrap',count:5},\
            {id:'ancient_debris',count:3},\
        ]}}},\
        {slot: 9,item:{id:'iron_ingot',count:64}},\
        {slot:10,item:{id:'iron_ingot',count:40}},\
        {slot:11,item:{id:'anvil',count:1}},\
        {slot:12,item:{id:'name_tag',count:2}},\
        {slot:13,item:{id:'tnt',count:8}},\
        {slot:14,item:{id:'ominous_bottle',count:1}},\
        {slot:15,item:{id:'ominous_bottle',count:3,components:{ominous_bottle_amplifier:2}}},\
        {slot:17,item:{id:'scaffolding',count:64}},\
        {slot:18,item:{id:'diamond',count:20}},\
        {slot:19,item:{id:'emerald',count:12}},\
        {slot:20,item:{id:'coal',count:64}},\
        {slot:21,item:{id:'obsidian',count:50}},\
        {slot:22,item:{id:'bee_nest',count:1,components:{bees:[\
            {min_ticks_in_hive:400,ticks_in_hive:1000,entity_data:{id:"bee"}},\
            {min_ticks_in_hive:400,ticks_in_hive:1000,entity_data:{id:"bee"}},\
            {min_ticks_in_hive:400,ticks_in_hive:1000,entity_data:{id:"bee"}},\
        ]}}},\
        {slot:23,item:{id:'tadpole_bucket',count:1}},\
        {slot:24,item:{id:'tadpole_bucket',count:1}},\
        {slot:25,item:{id:'tadpole_bucket',count:1}},\
        {slot:26,item:{id:'scaffolding',count:52}},\
    ]]
    /item replace entity @p inventory.5 with shulker_box[container=[\
        {slot: 0,item:{id:'spectral_arrow',count:64}},\
        {slot: 1,item:{id:'crossbow',count:1,components:{enchantments:{levels:{piercing:4}}}}},\
        {slot: 2,item:{id:'bow',count:1}},\
        {slot: 3,item:{id:'fishing_rod',count:1}},\
        {slot: 4,item:{id:'spyglass',count:1}},\
        {slot: 5,item:{id:'mace',count:1}},\
        {slot: 6,item:{id:'ghast_tear',count:4}},\
        {slot: 7,item:{id:'blaze_rod',count:6}},\
        {slot: 8,item:{id:'breeze_rod',count:1}},\
        {slot: 9,item:{id:'string',count:60}},\
        {slot:10,item:{id:'bone',count:10}},\
        {slot:11,item:{id:'phantom_membrane',count:1}},\
        {slot:12,item:{id:'brown_mushroom',count:2}},\
        {slot:13,item:{id:'red_mushroom',count:1}},\
        {slot:14,item:{id:'netherrack',count:64}},\
        {slot:15,item:{id:'soul_sand',count:4}},\
        {slot:16,item:{id:'wither_skeleton_skull',count:3}},\
        {slot:17,item:{id:'cobweb',count:1}},\
        {slot:18,item:{id:'brewing_stand',count:4}},\
        {slot:19,item:{id:'pufferfish',count:2}},\
        {slot:20,item:{id:'spider_eye',count:1}},\
        {slot:21,item:{id:'nether_wart',count:4}},\
        {slot:22,item:{id:'slime_ball',count:64}},\
        {slot:23,item:{id:'slime_ball',count:48}},\
        {slot:24,item:{id:'azure_bluet',count:4}},\
        {slot:25,item:{id:'ender_pearl',count:8}},\
    ]]
    /item replace entity @p inventory.0 with diamond_hoe[enchantments={levels:{efficiency:3}}]
    /item replace entity @p inventory.1 with golden_carrot 20
    /item replace entity @p inventory.2 with golden_apple 8
    /item replace entity @p inventory.3 with enchanted_golden_apple 6
    /item replace entity @p inventory.6 with sugar_cane 12
    /item replace entity @p inventory.7 with lead 8
    /item replace entity @p inventory.8 with hay_block 20
    /item replace entity @p inventory.9 with oak_log 34
    /item replace entity @p inventory.10 with furnace 4
    /item replace entity @p inventory.11 with cobblestone 32
    /item replace entity @p inventory.12 with totem_of_undying
    /item replace entity @p inventory.13 with flint_and_steel
    /item replace entity @p inventory.16 with gunpowder 16
    /item replace entity @p inventory.17 with firework_rocket 36
    /item replace entity @p inventory.18 with milk_bucket
    /item replace entity @p inventory.19 with milk_bucket
    /item replace entity @p inventory.20 with crafting_table 4
    /item replace entity @p inventory.21 with dirt 20
    /item replace entity @p inventory.25 with firework_rocket 64
    /item replace entity @p inventory.26 with firework_rocket 64
    /item replace entity @p weapon.offhand with bread 60
